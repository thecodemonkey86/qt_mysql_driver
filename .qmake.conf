# This file detaches this sub-tree from the rest of qtbase,
# so it can be configured stand-alone.
# Of course, under normal circumstances, this _is_ part of qtbase,
# so we have to make some contortions to restore normality.

isEmpty(_QMAKE_CONF_): return()  # Pre-scan during spec loading.

SQLDRV_SRC_TREE = $$dirname(_QMAKE_CONF_)
QTBASE_SRC_TREE = $$section(SQLDRV_SRC_TREE, /, 0, -4)

QTBASE_BLD_TREE = $$shadowed($$QTBASE_SRC_TREE)
!isEmpty(QTBASE_BLD_TREE):exists($$QTBASE_BLD_TREE/.qmake.cache) {
    # This tricks qt_build_config.prf and qt_build_paths.prf
    _QMAKE_CONF_ = $$QTBASE_SRC_TREE/.qmake.conf
} else {
    CONFIG += sqldrivers_standalone
}

include($$QTBASE_SRC_TREE/.qmake.conf)
